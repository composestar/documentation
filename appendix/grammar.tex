\chapter{Grammar}
This is the full design of the grammar. The current used grammar can differ from this one.

\begin{lstlisting}[label=lst::proposedgrammar,style=listing,language=ebnf]

Concern = 'concern' Identifier [ConcernParameters] ['in' Namespace] ConcernBlock;
ConcernParameters = '(' Identifier ':' Type {',' Identifier ':' Type } ')';
Type = Fqn;
Fqn = Identifier {'.' Identifier};
Namespace = Fqn;
ConcernBlock = '{' {FilterModule} [SuperImposition] [Implementation] '}';

FilterModule = 'filtermodule' Identifier [FilterModuleParameters] '{' [Internals] [Externals] [Conditions] [InputFilters] [OutputFilters] '}';
FilterModuleParameters = '(' ParameterDefinition {',' ParameterDefinition} ')';
ParameterDefinition = Parameter | ParameterList;
Parameter = '?' Identifier;
ParameterList = '??' Identifier;

Internals = 'internals' {Internal ';'};
Internal = Identifier {',' Identifier} ':' Type;
Externals = 'externals' {Identifier ':' Type ['=' InitialisationExpression] ';'};
InitialisationExpression = Fqn '(' [ArgumentList] ')';
ArgumentList = IdentifierOrFMParam {',' IdentifierOrFMParam};
IdentifierOrParameter = Identifier | Parameter;
Conditions = 'conditions' {Identifier ':' InitialisationExpression ';'};

InputFilters = 'inputfilters' FilterSet;
OutputFilters = 'outputfilters' FilterSet;

FilterSet = Filter {FilterOperator Filter};
FilterOperator = ';';
Filter = Identifier ':' FilterType ['(' [ArgumentList] ')'] '=' '{' FilterElements '}';
FilterType = Identifier;
FilterElements = FilterElement {FilterElementOperator FilterElement};
FilterElementOperator = ',';

FilterElement = [ConditionExpression MatchingOperator] MatchingPatternSet;

ConditionExpression = AndExpression ['|' ConditionExpression];
AndExpression = UnaryExpression ['&' AndExpression];
UnaryExpression = ['!'] ( Operand | '(' ConditionExpression ')' );
Operand = Identifier | 'True' | 'False';

MatchingOperator = '=>' | '~>';
MatchingPatternSet = MatchingPart [SubstitutionPart];
MatchingPart = '{' MatchingPattern {',' MatchingPattern} '}' | MatchingPattern;
MatchingPattern = '[' TargetSelector ']' | '<' TargetSelector '>';
SubstitutionPart = TargetSelector;
TargetSelector = [Target '.'] Selector;
Target = '*' |IdentifierOrParameter;
Selector = '*' | IdentifierOrParameter | ParameterList;

Superimposition = '{' [Conditionals] [Selectors] [FilterModuleBindings] [AnnotationBindings] [Constraints] '}';

Conditionals = 'conditions' {SiCondition ';'};
SiCondition = Identifier ':' Fqn ['(' Arguments ')']

Superimposition = '{' [Conditionals] [Selectors] [FilterModuleBindings] [AnnotationBindings] [Constraints] '}';
Selectors = 'selectors' { Identifier '=' '{' ( LegacyExpr | PredicateExpr) '}' ';' };
LegacyExpr = '*' ( ':' | '=' ) Fqn;
PredicateExpr = Identifier '|' ? prolog expression ?;

FilterModuleBindings = 'filtermodules' {FMBinding ';'};
FMBinding = [ConditionId '=>'] SelectorId WeaveOp ( FilterModules | '{' FilterModules '}' );
WeaveOp = '<-';
ConditionId = Identifier;
SelectorId = Identifier;
FilterModules = FilterModuleRefParam {',' FilterModuleRefParam};
FilterModuleRefParam = FilterModuleRef ['(' FmParam {',' FmParam} ')'];
FilterModuleRef = [Fqn '::'] Identifier;
FmParam = FmParamList | Fqn;
FmParamList = '{' Fqn { ',' Fqn } '}';

AnnotationBindings = 'annotations' {AnnotationBinding ';'};
AnnotationBinding = SelectorId WeaveOp ( Annotations | '{' Annotations '}' );
Annotation = Fqn { ',' Fqn };

Constraints = 'constaints' {Constraint ';'}
Constraint = Identifier '(' FilterModuleRef ',' FilterModuleRef ')';

Implementation = 'implementation' ('by' ClassName ';') | ( 'in' Language 'by' ClassName 'as' FileName SourceCode );
ClassName = Identifier;
Language = Identifier;
FileName = '"' ? just a filename with extention ? '"';
SourceCode = '{' ? the source code ? '}';

\end{lstlisting}